buildscript {
    dependencies {
        classpath 'org.postgresql:postgresql:42.2.12'
    }
}

plugins {
    id 'com.commercehub.cucumber-jvm' version '0.11'
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'com.github.sherter.google-java-format' version '0.8'
    id 'java'
}

group = 'com.myprod.mymessanger'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
      extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

repositories {
    mavenCentral()
}

sourceSets {
  cucumber {
    resources {
      srcDir file('test/resources')
    }
  }
}

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // PostgreSQL driver
    runtimeOnly 'org.postgresql:postgresql:42.2.12'

    // JWT
    compile 'io.jsonwebtoken:jjwt-api:0.10.7'
    runtime 'io.jsonwebtoken:jjwt-impl:0.10.7'

    /** Data Converters */

    /**
     * ModelMapper
     *
     * doc - http://modelmapper.org/
     */
    compile 'org.modelmapper:modelmapper:2.3.0'
    compile 'org.modelmapper.extensions:modelmapper-jackson:2.3.0'

    // Jackson
    compile 'io.jsonwebtoken:jjwt-jackson:0.10.7'

    compile "org.flywaydb:flyway-core:6.4.4"

    /** Tests */

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testImplementation 'org.springframework.security:spring-security-test'

    // Cucuber
    testImplementation 'io.cucumber:cucumber-core:6.1.0'
    testImplementation 'io.cucumber:cucumber-java:6.1.0'
    testImplementation 'io.cucumber:cucumber-junit:6.1.0'
    testImplementation 'io.cucumber:cucumber-spring:6.1.0'

    // Faker
    testImplementation 'com.github.javafaker:javafaker:1.0.2'

    // Swagger
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.7.0'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.7.0'

    testImplementation 'org.postgresql:postgresql:42.2.12'

    // JWT
    testImplementation 'io.jsonwebtoken:jjwt-api:0.10.7'
    testImplementation 'io.jsonwebtoken:jjwt-impl:0.10.7'

    testImplementation 'org.apache.httpcomponents:httpclient:4.5.12'
}

build.dependsOn(tasks.googleJavaFormat)
build.dependsOn(tasks.javadoc)
verifyGoogleJavaFormat.mustRunAfter(tasks.googleJavaFormat)

test {
    useJUnitPlatform()
    testLogging {
      events 'passed', 'skipped', 'failed'
    }
}

task cucumber() {
  dependsOn assemble, compileTestJava
  doLast {
    javaexec {
      main = 'io.cucumber.core.cli.Main'
      classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
      args = ['--plugin', 'pretty', '--glue', 'com.myprod.mymessenger.user.manager.cucumber', 'src/test/resources/features']
    }
  }
}
